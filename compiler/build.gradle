plugins {
    id 'java'
    id 'application'
    id "org.jetbrains.kotlin.jvm"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

targetCompatibility = 11
sourceCompatibility = 11


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

def prog8version = rootProject.file('compiler/res/version.txt').text.trim()

dependencies {
    implementation project(':compilerAst')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    // implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.jetbrains.kotlinx:kotlinx-cli:0.3.2'

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

configurations.all {
    exclude group: 'com.ibm.icu', module: 'icu4j'
    exclude group: "org.antlr", module: "antlr4"
}

configurations {
    // strange antlr plugin issue, see https://github.com/gradle/gradle/issues/820
    // this avoids linking in the complete antlr binary jar
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}


compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        useIR = true
        // verbose = true
        // freeCompilerArgs += "-XXLanguage:+NewInference"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
        useIR = true
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["${project.projectDir}/src"]
        }
        resources {
            srcDirs = ["${project.projectDir}/res"]
        }
    }
    test {
        java {
            srcDirs = ["${project.projectDir}/test"]
        }
	resources {
            srcDirs = ["${project.projectDir}/res"]
        }
    }
}

startScripts.enabled = true

application {
    mainClass = 'prog8.CompilerMainKt'
    applicationName = 'p8compile'
}

artifacts {
    archives shadowJar
}


shadowJar {
    archiveBaseName = 'prog8compiler'
    archiveVersion = prog8version
    // minimize()
}


test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "skipped", "failed"
    }
}
